# Once just v1.39.0 is widely deployed, simplify with the `read` function.
NIGHTLY_VERSION := trim(read(justfile_directory() / "nightly-version"))

_default:
  @just --list

# Install rbmt (Rust Bitcoin Maintainer Tools).
@_install-rbmt:
  cargo install --quiet --git https://github.com/rust-bitcoin/rust-bitcoin-maintainer-tools.git --rev $(cat {{justfile_directory()}}/rbmt-version) cargo-rbmt

# Cargo check everything.
check:
  cargo check --all --all-targets --all-features

# Cargo build everything.
build:
  cargo build --all --all-targets --all-features

# Test everything.
test:
  cargo test --all-targets --all-features

test-invalid:
  cargo test --test test_vectors test_invalid_vectors -- --no-capture

test-valid:
  cargo test --test test_vectors test_valid_vectors -- --no-capture

# Lint everything.
lint:
  cargo +{{NIGHTLY_VERSION}} clippy --all-targets --all-features -- --deny warnings

# Run cargo fmt
fmt:
  cargo +{{NIGHTLY_VERSION}} fmt --all

# Examples
[group('viewer')]
view:
  cargo r -p psbt-viewer

[group('hardware')]
hardware:
  cargo r -p hardware-signer --bin hardware-signer --features=gui

[group('multi')]
multi:
  cargo r -p multi-signer

[group('multi')]
multi-cli:
  cargo r -p multi-signer -- --cli

[group('hardware')]
hardware-demo:
  cargo r -p hardware-signer -- --demo-flow

[group('hardware')]
hardware-attack:
  cargo r -p hardware-signer -- --demo-flow --auto-read --auto-approve --attack

[group('uniffi')]
uniffi: uniffi-setup uniffi-test uniffi-example

[group('uniffi')]
uniffi-setup:
  pip install -e crates/spdk-uniffi

[group('uniffi')]
uniffi-test:
  pytest crates/spdk-uniffi/tests

[group('uniffi')]
uniffi-example:
  python crates/spdk-uniffi/examples/simple_example.py

# Generate documentation.
docsrs *flags:
  RUSTDOCFLAGS="--cfg docsrs -D warnings -D rustdoc::broken-intra-doc-links" cargo +{{NIGHTLY_VERSION}} doc --all-features {{flags}}

# Run CI tasks with rbmt.
[group('ci')]
@ci task toolchain="stable" lock="recent": _install-rbmt
  RBMT_LOG_LEVEL=quiet rustup run {{toolchain}} cargo rbmt --lock-file {{lock}} {{task}}

# Test crate.
[group('ci')]
ci-test: (ci "test stable")

# Lint crate.
[group('ci')]
ci-lint: (ci "lint" NIGHTLY_VERSION)

# Bitcoin core integration tests.
[group('ci')]
ci-integration: (ci "integration")
